    .cpu arm7tdmi
    .section .header
_start:
    @ ROMヘッダー
    b       start_vector     @ 0x00 エントリーポイント

    @ 0x04 起動画面に出てくる任天堂のロゴデータ
    .byte   0x24, 0xFF, 0xAE, 0x51, 0x69, 0x9A, 0xA2, 0x21, 0x3D, 0x84, 0x82, 0x0A, 0x84, 0xE4, 0x09, 0xAD, 0x11, 0x24, 0x8B, 0x98, 0xC0, 0x81, 0x7F, 0x21, 0xA3, 0x52, 0xBE, 0x19, 0x93, 0x09, 0xCE, 0x20, 0x10, 0x46, 0x4A, 0x4A, 0xF8, 0x27, 0x31, 0xEC, 0x58, 0xC7, 0xE8, 0x33, 0x82, 0xE3, 0xCE, 0xBF, 0x85, 0xF4, 0xDF, 0x94, 0xCE, 0x4B, 0x09, 0xC1, 0x94, 0x56, 0x8A, 0xC0, 0x13, 0x72, 0xA7, 0xFC, 0x9F, 0x84, 0x4D, 0x73, 0xA3, 0xCA, 0x9A, 0x61, 0x58, 0x97, 0xA3, 0x27, 0xFC, 0x03, 0x98, 0x76, 0x23, 0x1D, 0xC7, 0x61, 0x03, 0x04, 0xAE, 0x56, 0xBF, 0x38, 0x84, 0x00, 0x40, 0xA7, 0x0E, 0xFD, 0xFF, 0x52, 0xFE, 0x03, 0x6F, 0x95, 0x30, 0xF1, 0x97, 0xFB, 0xC0, 0x85, 0x60, 0xD6, 0x80, 0x25, 0xA9, 0x63, 0xBE, 0x03, 0x01, 0x4E, 0x38, 0xE2, 0xF9, 0xA2, 0x34, 0xFF, 0xBB, 0x3E, 0x03, 0x44, 0x78, 0x00, 0x90, 0xCB, 0x88, 0x11, 0x3A, 0x94, 0x65, 0xC0, 0x7C, 0x63, 0x87, 0xF0, 0x3C, 0xAF, 0xD6, 0x25, 0xE4, 0x8B, 0x38, 0x0A, 0xAC, 0x72, 0x21, 0xD4, 0xF8, 0x07   
    
    .space  12               @ 0xA0 ゲームタイトル
    .space  4                @ 0xAC ゲームコード
    .space  2                @ 0xB0 メーカーコード
    .byte   0x96             @ 0xB2 マジックナンバー
    .byte   0                @ 0xB3 ユニットコード
    .byte   0                @ 0xB4 デバイスタイプ
    .space  7                @ 0xB5 予約領域
    .byte   0                @ 0xBC ソフトウェアバージョン
    .byte   0x51             @ 0xBD ヘッダのチェックサム
    .space  2                @ 0xBE 予約領域

    @ マルチブート用の追加ヘッダー
    b       start_vector     @ 0xC0 マルチブート用のエントリーポイント
    .byte   0                @ 0xC4 ブートモード
    .byte   0                @ 0xC5 スレーブID
    .space  26               @ 0xC6 未使用領域
    b       start_vector     @ 0xE0 JOYBUS用のエントリーポイント
start_vector:
    msr     cpsr,  0b11011111           @ システムモードに切り替え
    ldr     sp,    SP_SYS               @ スタックポインタの設定

    @ .bssセクションの初期化
    ldr     r0,    =__bss_start__       @
    ldr     r1,    =__bss_end__         @
    bl      MemZeroFill                 @ 関数を呼び出す
    
    @ .dataセクションのコピー
    ldr     r0,    =__data_start__
    ldr     r1,    =__data_end__
    ldr     r2,    =__data_rom_start__
    bl      MemCpy

    b       main
SP_SYS:
    .word   0x03008000 - 0x100

@ メモリのゼロ埋め関数
@ start_ptrからend_ptr-1までの領域を0で埋めます
@ r0=start_ptr, r1=end_ptr
MemZeroFill: 
    mov     r2, #0
MemZeroFill_loop1:
    cmp     r0, r1                      @ r0, r1を比較してフラグを更新
    beq     MemZeroFill_loopEnd1        @ ループ終了
    strb    r2, [r0], #1                @ Memory[r0]=r2, r0+=1
    b       MemZeroFill_loop1           @ ループの最初へ
MemZeroFill_loopEnd1:
    bx      lr                          @ 関数から戻る

@ メモリコピー関数
@ dest_start_ptrからdest_end_ptr-1までの領域を
@ source_start_ptrから始まる領域にコピーします
@ r0=dest_start_ptr, r1=dest_end_ptr, r2=source_start_ptr
MemCpy:
MemCpy_loop1:
    cmp     r0, r1                      @ r0, r1を比較してフラグを更新
    beq     MemCpy_loopEnd1             @ ループ終了
    ldrb    r3, [r2], #1                @ コピー元から読み込み
    strb    r3, [r0], #1                @ コピー先へ書き込み
    b       MemCpy_loop1                @ ループの最初へ
MemCpy_loopEnd1:
    bx      lr                          @ 関数から戻る



    .space  200              @ サイズが小さいとMultibootで送れないので大きくする
